shell.prefix("set -eo pipefail; echo BEGIN at $(date); ")
shell.suffix("; exitstat=$?; echo END at $(date); echo exit status was $exitstat; exit $exitstat")

configfile: "Chipseq.yaml"

SAMPLES = config['SAMPLE']
CONTROLS = [sample for sample in SAMPLES if config['control'] in sample]
CASE = [sample for sample in SAMPLES if config['case'] in sample]

from itertools import product

rule all:
    input:
        expand("03.Bam/{sample}.rmdup.bam", sample = SAMPLES),
        expand("04.BW/{sample}.bw", sample = SAMPLES),
        expand("05.Cpeaks/{case}_vs_{control}_PQ_peaks.narrowPeak", zip, case = CASE, control = CONTROLS)

rule fastp_trim:
    input:
        raw1 = "01.Raw_seq/{sample}_1.fq.gz",
        raw2 = "01.Raw_seq/{sample}_2.fq.gz"
    output:
        paired1 = "02.Paired/{sample}_1.paired.fq.gz",
        paired2 = "02.Paired/{sample}_2.paired.fq.gz",
        json = "02.Paired/{sample}.json",
        html = "02.Paired/{sample}.html"
    threads:
        config["fastp_trim"]["cpu"]
    log:
        "log/paired/{sample}.log"
    shell:
        "fastp -i {input.raw1} -o {output.paired1} -I {input.raw2} -O {output.paired2} -j {output.json} -h {output.html} -c -w {threads} > {log} 2>&1" 

rule bowtie2_map:
    input:
        paired1 = "02.Paired/{sample}_1.paired.fq.gz",
        paired2 = "02.Paired/{sample}_2.paired.fq.gz"
    output:
        sam = "03.Bam/{sample}.sam"
    params:
        index = "00.Ref/bowtie2/genome"
    threads:
        config["bowtie2_map"]["cpu"]
    log:
        "log/bowtie2/{sample}.log"
    shell:
        "bowtie2 -p {threads} -x {params.index} -1 {input.paired1} -2 {input.paired2} -S {output.sam} > {log} 2>&1"

rule sam2bam:
    input:
        sam = "03.Bam/{sample}.sam"
    output:
        bam = "03.Bam/{sample}.sort.bam"
    threads:
        config["sam2bam"]["cpu"]
    params:
        config["filterThreshold"]
    shell:
        "samtools view -@ {threads} -h -F 1804 -q {params} {input.sam}|samtools sort -@ {threads} -o {output.bam}"

rule MarkDup:
    input:
        "03.Bam/{sample}.sort.bam"
    output:
        rmdup = "03.Bam/{sample}.rmdup.bam",
        metrics = "03.Bam/{sample}.metrics"
    threads:
        config["MarkDup"]["cpu"]
    log:
        "log/picard/{sample}.log"
    shell:
        "java -jar /opt/picard.jar MarkDuplicates I={input} O={output.rmdup} M={output.metrics} REMOVE_DUPLICATES=true VALIDATION_STRINGENCY=LENIENT > {log} 2>&1"

rule bam_index:
    input:
        "03.Bam/{sample}.rmdup.bam"
    output:
        "03.Bam/{sample}.rmdup.bam.bai"
    threads:
        config["bam_index"]["cpu"]
    shell:
        "samtools index {input}"

rule bam2bw:
    input:
        bam = "03.Bam/{sample}.rmdup.bam",
        bai = "03.Bam/{sample}.rmdup.bam.bai"
    output:
        "04.BW/{sample}.bw"
    threads:
        config["bam2bw"]["cpu"]
    params:
        binsize = config["bam2bw"]["binsize"],
        normalizemethod = config["bam2bw"]["normalizemethod"]
    log:
        "log/bW/{sample}.log"
    shell:
        "bamCoverage -b {input.bam} --binSize {params.binsize} --normalizeUsing {params.normalizemethod} -o {output}"

rule callpeak:
    input:
        control = "03.Bam/{control}.rmdup.bam",
        case = "03.Bam/{case}.rmdup.bam"
    output:
        "05.Cpeaks/{case}_vs_{control}_PQ_peaks.narrowPeak"
    params:
        name = "{case}_vs_{control}_PQ",
        outdir = "05.Cpeaks/"
    threads:
        config["callpeak"]["cpu"]
    log:
        "log/macs2/{case}_vs_{control}.log"
    shell:
        "macs2 callpeak -t {input.case} -c {input.control} --outdir {params.outdir} -f BAMPE -g 374471240 -n {params.name} -p 0.01 > {log} 2>&1"